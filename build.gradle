plugins {
	id 'org.quiltmc.loom' version '1.0+'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven { url 'https://masa.dy.fi/maven' }
	maven {
		url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
	}
	maven {
		url "https://maven.jamieswhiteshirt.com/libs-release"
		content {
			includeGroup "com.jamieswhiteshirt"
		}
	}
	maven {
		name "cardinal components, impersonate, player ability lib, trinkets"
		url "https://maven.ladysnake.org/releases"
	}
	maven {
		url "https://maven.ladysnake.org/snapshots"
		content {
			includeGroup("org.ladysnake")
			includeGroup("xyz.amymialee")
		}
	}
	maven {
		name "modrinth"
		url "https://api.modrinth.com/maven"
		content {
			includeGroup('maven.modrinth')
		}
	}
	maven {
		url "https://jitpack.io"
		content {
			includeGroupByRegex "com\\.github\\..*"
		}
	}
	maven {
		url "https://maven.cph101.com/releases"
		// Impaled snapshot, not hosted on ladysnake maven or jitpack yet
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.layered {
		mappings "org.quiltmc:quilt-mappings:${minecraft_version}+build.${quilt_mappings}:intermediary-v2"
	}
	modImplementation "org.quiltmc:quilt-loader:${project.loader_version}"

	modImplementation "org.quiltmc.quilted-fabric-api:quilted-fabric-api:${quilted_fabric_api_version}-${minecraft_version}"
	modImplementation "software.bernie.geckolib:geckolib-fabric-1.20.1:${project.geckolib_version}"

	modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cardinal_components_version}"
	include "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cardinal_components_version}"
	modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${project.cardinal_components_version}"
	include "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${project.cardinal_components_version}"
	modImplementation "org.ladysnake:effective:2.2.1+1.20.1"
	modRuntimeOnly "com.github.gnembon:fabric-carpet:${project.carpet_core_version}"

	modImplementation include("com.github.cph101:reach-entity-attributes:1.20.1-SNAPSHOT")

	include(implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:0.3.5")))

	modCompileOnlyApi("org.ladysnake:Impaled:2.0.0") { transitive false } // 1.20.1 snapshot
}
configurations.all {
	resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}
loom {
	accessWidenerPath = file("src/main/resources/mason.accesswidener")
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
